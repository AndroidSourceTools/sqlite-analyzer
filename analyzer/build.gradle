repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile 'org.xerial:sqlite-jdbc:3.8.7'

    compile gradleApi()
    compile localGroovy()

    testCompile 'junit:junit:4.11'
}

group = 'com.novoda'
version = "0.0.1-SNAPSHOT"

ext {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            // don't sign snapshots for now
            if (!version.endsWith('SNAPSHOT')) {
                beforeDeployment { deployment -> signing.signPom(deployment) }
            }

            if (project.hasProperty('nexusUsername')) {
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
            }
            uniqueVersion = false

            pom.artifactId = 'sqlite-analyzer'
            pom.project {

                parent {
                    groupId 'org.sonatype.oss'
                    artifactId 'oss-parent'
                    version '7'
                }

                name 'sqliteAnalyzer Gradle Plugin'
                packaging 'jar'
                description 'Code generation for sqlite database access.'
                url 'https://github.com/novoda/sqliteAnalyzer'

                scm {
                    url 'https://github.com/novoda/sqliteAnalyzer'
                    connection 'scm:https://novoda@github.com/novoda/sqliteAnalyzer.git'
                    developerConnection 'scm:git@github.com:novoda/sqliteAnalyzer.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'devisnik'
                        name 'Volker Leck'
                    }
                }
            }
        }
    }
}


